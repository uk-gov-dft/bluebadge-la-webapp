buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://artifactory.does.not.exist/artifactory/gradle-dev-local'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
        classpath 'org.owasp:dependency-check-gradle:3.3.1'
    }
}

plugins {
    id 'com.github.sherter.google-java-format' version '0.6'
    id "org.sonarqube" version "2.6.2"
    id "com.moowork.node" version "1.2.0"
    id "com.moowork.gulp" version "1.2.0"
    id "com.jfrog.artifactory" version "4.5.4"
    id "com.gorylenko.gradle-git-properties" version "1.5.1"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'org.owasp.dependencycheck'

//apply plugin: 'findbugs'

def getTheVersion() {
    def workingDir = new File("${project.projectDir}")
    def version = 'git describe --abbrev=0'.execute(null, workingDir)
    version.waitFor()
    def versionSuffix = "UnknownA"
    def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
    result.waitFor()
    if (result.exitValue() == 0) {
        def gitBranch = result.text.trim().replaceAll('/','_')
        if (gitBranch == 'master') {
            versionSuffix = ''
        } else if (gitBranch == 'develop') {
            versionSuffix = '-SNAPSHOT'
        } else {
            versionSuffix = '-' + gitBranch
        }

    }
    return "${version.text.trim()}${versionSuffix}"
}

def findUText() {
    println("Looking for th:utext")
    def hasError = false;

    for (File file : sourceSets.main.resources) {
        if (file.getName().endsWith(".html")) {
            file.eachLine { line ->
                if (line.toLowerCase().contains("th:utext")) {
                    println file.getAbsolutePath() + " - " + line
                    hasError = true
                }
            }
        }

    }
    return hasError
}

task outputComputedVersion {
    def computedVersion = new File('VERSION-computed')
    computedVersion.delete()
    computedVersion.write(getTheVersion())
    println(getTheVersion())
}


group = 'uk.gov.dft.bluebadge.webapp.la'
version = getTheVersion()
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://artifactory.does.not.exist/artifactory/gradle-dev-local'
    }
}

publishing {
    publications {
        project_webapp(MavenPublication) {
            from components.java
            artifactId "la-webapp"
        }
    }
}

artifactory {
    contextUrl = "https://artifactory.does.not.exist/artifactory"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'gradle-dev-local'
            maven = true
        }

        publishArtifacts=true
        publishPom=true
        defaults {
            publications('project_webapp')
            publishArtifacts=true
            publishPom=true
        }
    }

    resolve {
        repository {
            repoKey = 'gradle-release'
            maven = true

        }
    }

}

archivesBaseName = "la-webapp"

configurations.all {
    resolutionStrategy {
        // don't cache changing modules at all
        cacheChangingModulesFor 0, 'seconds'
    }
    exclude group: 'commons-logging', module: 'commons-logging'
}

dependencies {
    ext.commonVersion = '0.7.10-SNAPSHOT'

    compile( group: 'uk.gov.dft.bluebadge', name: 'bluebadge-common', version: "${commonVersion}", changing: true ){
        exclude group: 'org.mybatis.spring.boot', module: 'mybatis-spring-boot-starter'
    }

    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.1.2.RELEASE')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.session:spring-session-data-redis')
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-sleuth', version: '2.0.2.RELEASE'
    compile group: 'io.micrometer', name: 'micrometer-core', version: '1.1.4'
    compile group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: '1.1.4'

    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.thymeleaf.extras:thymeleaf-extras-springsecurity5')
    compile('nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0')
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
    compile('org.webjars:jquery:3.2.1')
    //compile('org.webjars:bootstrap:3.3.7')

    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('io.springfox:springfox-swagger-ui:2.7.0')

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile('org.apache.commons:commons-lang3:3.7')
    compileOnly ('org.projectlombok:lombok:1.16.22')

    //hot swapping, disable cache for template, enable live reload
    runtime('org.springframework.boot:spring-boot-devtools')
    runtime group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '5.2'
    runtime group: 'ch.qos.logback.contrib', name: 'logback-json-classic', version: '0.1.5'
    runtime group: 'ch.qos.logback.contrib', name: 'logback-jackson', version: '0.1.5'
    runtime group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.3'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile("org.assertj:assertj-core:3.9.1")
    testCompile('com.jayway.restassured:rest-assured:2.9.0')
    testCompile( group: 'uk.gov.dft.bluebadge', name: 'bluebadge-common', version: "${commonVersion}", classifier: 'tests',  changing: true ){
        exclude group: 'org.mybatis.spring.boot', module: 'mybatis-spring-boot-starter'
    }
    testCompileOnly ('org.projectlombok:lombok:1.16.22')
}

jacoco {
    toolVersion = "0.8.1"
}

test {
    jacoco {
        excludes += ['.*uk.gov.dft.bluebadge.webapp.la.controller.request.*', '.*uk.gov.dft.bluebadge.webapp.la.controller.viewmodel.*', '.*uk.gov.dft.bluebadge.webapp.la.client.usermanagement.model.*','.*uk.gov.dft.bluebadge.webapp.la.exception.*']
    }
}

bootRun {
    sourceResources sourceSets.main
}

springBoot {
    buildInfo()
}

task checkUiResources {
    if (findUText()) throw new GradleException("Failed due to illegal utext found in html")
}

build.dependsOn gulp_default
build.finalizedBy(outputComputedVersion)

compileJava.dependsOn 'googleJavaFormat', 'checkUiResources'

